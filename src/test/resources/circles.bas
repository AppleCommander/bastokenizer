10 GOTO 100

20 REM DRAW CIRCLE ROUTINE
30 FOR A = 0 TO PT
40 X = X(A) * SZ:Y = Y(A) * SZ
50 HPLOT XO + X,YO + Y
60 HPLOT XO - X,YO + Y
70 HPLOT XO + X,YO - Y
80 HPLOT XO - X,YO - Y
90 NEXT A
95 RETURN

100 REM MAIN PROGRAM
110 HGR
115 C(0)=1:C(1)=2:C(2)=3:C(3)=5:C(4)=6:C(5)=7
120 HOME : VTAB 21: INVERSE : PRINT "JUST A MOMENT": NORMAL
130 PI = 3.14159
140 PT = 30: DIM X(PT),Y(PT)
150 FOR A = 0 TO PT
160 B = PI * (A / (PT * 2))
170 X(A) = SIN (B)
180 Y(A) = COS (B)
190 NEXT A
200 HOME : VTAB 21
210 FOR Q = 1 TO 100
215 C = 6 * RND(1) : HCOLOR= C(C)
220 SZ = 10 + (40 * RND (1))
230 XO = (279 - SZ*2) * RND (1) + SZ
240 YO = (159 - SZ*2) * RND (1) + SZ
250 GOSUB 30
260 NEXT Q
