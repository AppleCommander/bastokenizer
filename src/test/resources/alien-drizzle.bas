1 REM From Beagle Bros. Beagle Compiler
20  GOSUB 32000: HOME : INVERSE 
30  PRINT  CHR$(21)
50 Q =  -16384:BUZZ = Q +48:BEEP$ =  CHR$(7)
70  GR 
80  VTAB 23: HTAB 1: PRINT ":SAUCERS:";: HTAB 11: PRINT "::SHOTS::";: HTAB 21: PRINT "::HITS:::";: HTAB 31: PRINT ":PERCENT:"
85  VTAB 24: HTAB 1: PRINT ":::::::::";: HTAB 11: PRINT ":::::::::";: HTAB 21: PRINT ":::::::::";: HTAB 31: PRINT ":::00%:::";
88  GOSUB 370:SLUGHT = 35:XFALL = 1:YY = 0:HRZ = 0:SCRCLR =  INT( RND(1) *14) +1: GOSUB 800
90  VTAB 21: HTAB 1
95  PRINT " SPACE TO FIRE, ANY KEY TO STOP BULLET "
97  GOSUB 800
100  REM <CTRL-J><CTRL-J>GUN<CTRL-J>
110 GUNCLR =  INT( RND(1) *14) +1: IF GUNCLR = SCRCLR  THEN 110
115  COLOR= GUNCLR: HLIN 22,26 AT 39: HLIN 23,25 AT 38: VLIN 36,37 AT 24
120 K =  PEEK(Q): IF K < >160  AND K < >155  THEN 220
125  IF K = 155  THEN  NORMAL : VTAB 21: END 
130  IF SLUGHT < >35  THEN 140
132 SHOT = SHOT +1: GOSUB 610
135  GOSUB 670
140  COLOR= 15
150  IF  SCRN( 24,SLUGHT -1) = 0  THEN 200
151  REM <CTRL-J><CTRL-J>HIT!<CTRL-J>
152  PLOT 24,SLUGHT -1: COLOR= 0: PLOT 24,SLUGHT
161  COLOR= 9
162  PLOT 22,SLUGHT -3: PLOT 23,SLUGHT -2: PLOT 23,SLUGHT: PLOT 24,SLUGHT -1: PLOT 25,SLUGHT: PLOT 27,SLUGHT -2: VLIN SLUGHT -3,SLUGHT -2 AT 25
166  FOR I = 1 TO 9: GOSUB 620: NEXT :KILL = 1
170  COLOR= 0: FOR I = 22 TO 27: VLIN SLUGHT -3,SLUGHT AT I: NEXT 
180 HIT = HIT +1: GOSUB 670
190 YY = 1:XFALL = 0
195  POKE Q +16,0: GOSUB 240
200  PLOT 24,SLUGHT -1: COLOR= 0: PLOT 24,SLUGHT
210 SLUGHT = SLUGHT -1: IF SLUGHT >YFALL -3  THEN 220
211  COLOR= 0: VLIN 0,35 AT 24:SLUGHT = 35: POKE Q +16,0
220  GOSUB 240
230  GOTO 120
240  REM <CTRL-J><CTRL-J>SAUCER<CTRL-J>
250  COLOR= SCRCLR
270  HLIN HRZ +2,HRZ +3 AT YFALL: HLIN HRZ +1,HRZ +4 AT YFALL +1
280  COLOR= 0
290  PLOT HRZ +1,YFALL: PLOT HRZ,YFALL +1
300  IF KILL  THEN  PLOT HRZ +4,YFALL: HLIN HRZ,HRZ +5 AT YFALL -1: HLIN HRZ,HRZ +5 AT YFALL -2
305  IF YY = 0  THEN 310
306 N = 41 -YFALL: GOSUB 900
307 XFALL = XFALL + INT( RND(1) *3) -1: IF HRZ +XFALL <19  THEN XFALL = 1
308  IF HRZ +XFALL >25  THEN XFALL =  -1
310 HRZ = HRZ +XFALL:YFALL = YFALL +YY: IF HRZ >35  THEN 340
320  IF YFALL >38  THEN  GOSUB 390
330  RETURN 
340  HLIN 36,39 AT YFALL +1: HLIN 37,38 AT YFALL
360  GOSUB 800:SCRCLR =  INT( RND(1) *14) +1: IF SCRCLR = GUNCLR  THEN 360
370 SAUCER = SAUCER +1: GOSUB 670
380 HRZ = 0: RETURN 
390  REM <CTRL-J><CTRL-J>EXPLOSION<CTRL-J>
400  VTAB 21: HTAB HRZ: PRINT SWEAR$:KILL = 0
420  COLOR= 15
422  PLOT HRZ,37: PLOT HRZ +1,38: VLIN 35,37 AT HRZ +2: PLOT HRZ +2,39: VLIN 38,39 AT HRZ +3: VLIN 36,37 AT HRZ +4: PLOT HRZ +5,37
425  POP : PRINT BEEP$;BEEP$;BEEP$;BEEP$;
427  COLOR= SCRCLR
430  FOR I = 39 TO 6  STEP  -2
450  HLIN 19,29 AT I -1
460  HLIN 19,29 AT I: GOSUB 640
470  NEXT 
480  HLIN 29,33 AT 2: HLIN 20,23 AT 3: HLIN 28,34 AT 3: HLIN 18,24 AT 4: HLIN 27,35 AT 4: HLIN 17,35 AT 5: HLIN 16,35 AT 6: HLIN 15,35 AT 7
490  HLIN 16,36 AT 8: HLIN 13,37 AT 9: HLIN 12,38 AT 10: HLIN 11,38 AT 11: HLIN 11,38 AT 12: HLIN 10,38 AT 13: HLIN 10,38 AT 14: HLIN 10,37 AT 15
500  HLIN 10,37 AT 16: HLIN 10,37 AT 17: HLIN 11,14 AT 18: HLIN 16,36 AT 18: HLIN 12,13 AT 19: HLIN 16,34 AT 19: HLIN 16,34 AT 20: HLIN 17,34 AT 21
510  HLIN 31,33 AT 22
540  FOR I = 1 TO 22
545  IF I - INT(I/2) *2 = 0  THEN  COLOR= SCRCLR
546  IF I - INT(I/2) *2 = 1  THEN  COLOR= GUNCLR
550  FOR J = 1 TO 2 *I: NEXT 
560 HRZ =  INT( RND(1) *40):YFALL =  INT( RND(1) *20) +20: GOSUB 640: PLOT HRZ,YFALL: NEXT 
566  COLOR= 0
570  FOR I = 0 TO 39: HLIN 0,39 AT I: FOR J = 1 TO 9: NEXT : NEXT : FOR J = 1 TO 222: NEXT 
580  POP : GOTO 88
590  FOR I = 1 TO 500: NEXT 
600  REM <CTRL-J><CTRL-J>NOISE<CTRL-J>
610  COLOR= 9: HLIN 23,25 AT 35: HLIN 22,23 AT 34: HLIN 25,26 AT 34
620  FOR J = 1 TO 10:S =  PEEK(BUZZ) - PEEK(BUZZ) - PEEK(BUZZ): NEXT 
630  COLOR= 0: HLIN 23,25 AT 35: HLIN 22,26 AT 34: RETURN 
640  FOR J = 1 TO 2
650 S =  PEEK(BUZZ) - PEEK(BUZZ) - PEEK(BUZZ) - PEEK(BUZZ) - PEEK(BUZZ) - PEEK(BUZZ) - PEEK(BUZZ): NEXT : RETURN 
660  REM <CTRL-J><CTRL-J>SCOREBOARD<CTRL-J>
670  VTAB 24: HTAB 4: PRINT SAUCER;: HTAB 14: PRINT SHOT;
680  HTAB 24: PRINT HIT;: IF   NOT HIT  THEN  RETURN 
690  HTAB 34: PRINT  INT((100 *HIT)/SAUCER);"%:";
700  RETURN 
800 YFALL = Y1:Y1 =  INT( RND(1) *25) +3: RETURN 
900  POKE 0, ABS(100 -(3 *N)): POKE 1,5
902  CALL 771
904  RETURN 
32000  FOR I = 771 TO 789: READ A: POKE I,A: NEXT : RETURN 
33333  DATA 173,48,192,136,208,4,198,1,240,8,202,208,246,166,0,76,3,3,96
